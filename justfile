project := "Claude Status.xcodeproj"
scheme := "Claude Status"
# Override deployment target for CI/older Xcode that doesn't know macOS 26.2
xcode_flags := "CODE_SIGN_IDENTITY=- CODE_SIGNING_ALLOWED=NO MACOSX_DEPLOYMENT_TARGET=15.0"
app_name := "Claude Status"
team_id := env_var_or_default("DEVELOPMENT_TEAM", "6ZWB9X826X")
# App group override for forks: team-prefixed groups (TEAMID.name) need no
# provisioning profile on macOS, unlike the group.* release identifier.
app_group := env_var_or_default("APP_GROUP_ID", "group.com.poisonpenllc.Claude-Status")

# Calculate version from git tags: tag + .devN for unreleased commits.
# The leading "v" is stripped, as release.yml does.
version := `tag=$(git describe --tags --abbrev=0 2>/dev/null || echo "0.0.0"); commits=$(git rev-list --count "$tag"...HEAD 2>/dev/null || echo "0"); tag="${tag#v}"; if [ "$commits" -gt 0 ]; then echo "$tag.dev$commits"; else echo "$tag"; fi`

# Build provenance shown by the app (popover banner, Settings), local builds only.
# The commit gets "-dirty" when tracked files differ from HEAD; the plugin
# submodule is ignored since build-plugin rewrites its binaries.
build_source := `git remote get-url origin 2>/dev/null | sed -E 's#^(git@github\.com:|https://github\.com/)##; s#\.git$##' || true`
build_branch := `git rev-parse --abbrev-ref HEAD 2>/dev/null || true`
build_commit := `sha=$(git rev-parse --short HEAD 2>/dev/null || true); [ -n "$sha" ] && ! git diff --quiet --ignore-submodules HEAD 2>/dev/null && sha="$sha-dirty"; echo "$sha"`
build_date := `date '+%Y-%m-%d %H:%M'`
build_info_flags := 'CS_BUILD_SOURCE="' + build_source + '" CS_BUILD_BRANCH="' + build_branch + '" CS_BUILD_COMMIT="' + build_commit + '" CS_BUILD_DATE="' + build_date + '"'

# Build the Rust plugin binaries and copy to the plugin scripts directory
build-plugin:
    cd claude-status-plugin && cargo build --release
    mkdir -p claude-status-plugin/plugins/claude-status/scripts
    cp claude-status-plugin/target/release/session-status claude-status-plugin/plugins/claude-status/scripts/
    cp claude-status-plugin/target/release/set-session-name claude-status-plugin/plugins/claude-status/scripts/
    codesign -fs - claude-status-plugin/plugins/claude-status/scripts/session-status
    codesign -fs - claude-status-plugin/plugins/claude-status/scripts/set-session-name

# Build debug configuration (unsigned, for CI and fast iteration)
build: build-plugin
    xcodebuild -project "{{project}}" -scheme "{{scheme}}" -configuration Debug build {{xcode_flags}} MARKETING_VERSION="{{version}}" {{build_info_flags}}

# Run all unit tests
test:
    xcodebuild -project "{{project}}" -scheme "{{scheme}}" -configuration Debug test \
        -only-testing:"Claude StatusTests" {{xcode_flags}}

# Run a single test class (e.g., just test-class SessionStateTests)
test-class class:
    xcodebuild -project "{{project}}" -scheme "{{scheme}}" \
        -only-testing:"Claude StatusTests/{{class}}" test {{xcode_flags}}

# Clean build artifacts
clean:
    xcodebuild -project "{{project}}" -scheme "{{scheme}}" clean {{xcode_flags}}

# Kill running app, copy signed debug build to /Applications, and relaunch.
# Uses Xcode automatic signing with Apple Development certificate.
swap: build-plugin
    #!/usr/bin/env bash
    set -euo pipefail
    build_dir="/tmp/claude-status-swap"
    xcodebuild -project "{{project}}" -scheme "{{scheme}}" -configuration Debug build \
        -derivedDataPath "$build_dir" \
        -allowProvisioningUpdates \
        MACOSX_DEPLOYMENT_TARGET=15.0 \
        CODE_SIGN_STYLE=Automatic \
        DEVELOPMENT_TEAM="{{team_id}}" \
        APP_GROUP_ID="{{app_group}}" \
        MARKETING_VERSION="{{version}}" \
        {{build_info_flags}}
    pkill -x "{{app_name}}" || true
    sleep 0.5
    rm -rf "/Applications/{{app_name}}.app"
    cp -R "$build_dir/Build/Products/Debug/{{app_name}}.app" "/Applications/{{app_name}}.app"
    open "/Applications/{{app_name}}.app"

# Show the calculated version
show-version:
    @echo "{{version}}"

# Sync the full plugin to the installed plugin cache and update the registry
sync-plugin: build-plugin
    rm -rf ~/.claude/plugins/cache/claude-status-marketplace/
    mkdir -p ~/.claude/plugins/cache/claude-status-marketplace/claude-status/{{version}}/
    rsync -a claude-status-plugin/plugins/claude-status/ \
        ~/.claude/plugins/cache/claude-status-marketplace/claude-status/{{version}}/
    codesign -fs - ~/.claude/plugins/cache/claude-status-marketplace/claude-status/{{version}}/scripts/session-status
    codesign -fs - ~/.claude/plugins/cache/claude-status-marketplace/claude-status/{{version}}/scripts/set-session-name
    python3 -c "\
    import json, pathlib; \
    p = pathlib.Path.home() / '.claude/plugins/installed_plugins.json'; \
    d = json.loads(p.read_text()); \
    key = 'claude-status@claude-status-marketplace'; \
    ver = '{{version}}'; \
    path = str(pathlib.Path.home() / '.claude/plugins/cache/claude-status-marketplace/claude-status' / ver); \
    entry = d.get('plugins', {}).get(key, [{}])[0]; \
    entry['installPath'] = path; \
    entry['version'] = ver; \
    d.setdefault('plugins', {})[key] = [entry]; \
    p.write_text(json.dumps(d, indent=2) + '\n')"
